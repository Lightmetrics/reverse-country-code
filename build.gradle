plugins {
    id 'jacoco'
    id 'java'
    id 'de.undercouch.download' version '4.0.0'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'me.champeau.gradle.jmh' version '0.4.8'
}

group = 'uk.recurse'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.1'
    testImplementation 'org.testng:testng:6.10'
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jmh {
    warmupIterations = 10
    iterations = 50
    fork = 1
}

task generateBaseline(type: JavaExec, dependsOn: testClasses) {
    main = 'uk.recurse.geocoding.reverse.BaselineGenerator'
    args "${projectDir}/src/test/resources"
    classpath = sourceSets.test.runtimeClasspath
}

task updateCountryInfo(type: Download) {
    src 'https://download.geonames.org/export/dump/countryInfo.txt'
    dest "${projectDir}/src/main/resources"
}

task downloadShapes(type: Download) {
    src 'https://download.geonames.org/export/dump/shapes_simplified_low.json.zip'
    dest new File(buildDir, 'shapes_simplified_low.json.zip')
}

task updateShapes(dependsOn: downloadShapes, type: Copy) {
    from zipTree(downloadShapes.dest)
    into "${projectDir}/src/main/resources"
}
