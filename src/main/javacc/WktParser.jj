options {
    STATIC = false;
    IGNORE_CASE = true;
}

PARSER_BEGIN( WktParser )
    package com.github.countrycode.reverse;

    import java.util.ArrayList;
    import java.util.List;

    class WktParser {
    }
PARSER_END( WktParser )

SKIP: { " " | "\t" | "\n" | "\r" }
TOKEN: { < REAL: ("-")? (["0"-"9"])+ ("." (["0"-"9"])*)? > }

Geometry parse(String id):
{
    Geometry geo;
}
{
    ( "MULTIPOLYGON" geo = multiPolygon(id) | "POLYGON" geo = polygon(id) )
    <EOF>
    {
        return geo;
    }
}

MultiPolygon multiPolygon(String id):
{
    List<Polygon> polys = new ArrayList<Polygon>();
}
{
    "(" { polys.add(polygon(id)); } ( "," { polys.add(polygon(id)); } )* ")"
    {
        return new MultiPolygon(id, polys);
    }
}

Polygon polygon(String id):
{
    List<Point> ring;
    List<Polygon> holes = new ArrayList<Polygon>();
}
{
    "(" ring = ring() ( "," { holes.add(new Polygon(ring())); } )* ")"
    {
        return new Polygon(id, ring, holes);
    }
}

List<Point> ring():
{
    List<Point> coordinates = new ArrayList<Point>();
}
{
    "(" point(coordinates) ( "," point(coordinates) )* ")"
    {
        return coordinates;
    }
}

void point(List<Point> coordinates):
{
    Double lat, lon;
}
{
    lon = real() lat = real()
    {
        coordinates.add(new Point(lat, lon));
    }
}

Double real():
{
    Token t;
}
{
    t = <REAL>
    {
        return Double.valueOf(t.image);
    }
}
